library OpioidCDSREC10PatientView version '2.0.1'

using FHIR version '4.0.1'

include FHIRHelpers version '4.0.1' called FHIRHelpers
include OpioidCDSCommon version '2.0.1' called Common
include OpioidCDSCommonConfig version '2.0.1' called Config
include OpioidCDSRoutines version '2.0.1' called Routines

code "Urine Drug Screening": '310627008' from Common.SNOMED display 'Urine drug screening (procedure)'

/*
**  Recommendation #10
**    When prescribing opioids for chronic pain, providers should use urine drug
**    testing before starting opioid therapy and consider urine drug testing at
**    least annually to assess for prescribed medications as well as other controlled
**    prescription drugs and illicit drugs (recommendation category: B, evidence type: 4)
**
**  When
**    Provider is prescribing an opioid analgesic with ambulatory misuse potential in the outpatient setting
**    Prescription is for treating chronic pain.
**    Opioid review is useful for this patient:
**      Patient is 18 or over
**      Patient does not have findings indicating limited life expectancy
**      Patient does not have orders for therapies indicating end of life care
**      Patient is not undergoing active cancer treatment:
**        Patient has had at least 2 encounters within the past year with any diagnosis of cancer
**    Urine drug screening has not been performed in last 12 months
**  Then
**    Recommend urine drug screening
**      Will perform urine screening
**      Not for chronic pain management, snooze 3 months
**      N/A - see comment, snooze 3 months
**
*/

// META: Plan Definition: http://fhir.org/guides/cdc/opioid-cds-r4/PlanDefinition/opioid-cds-10-patient-view

context Patient

define "Lookback Year":
  Interval[Today() - 12 months - 1 days, Today() - 1 day]

define "Patient Is Being Prescribed Opioid Analgesic with Ambulatory Misuse Potential":
  exists (
    Common."Active Ambulatory Opioid Rx" AmbulatoryOpioidPrescription
      where Routines."Is Chronic Pain Prescription?"( AmbulatoryOpioidPrescription )
  )

define "Is Recommendation Applicable?":
  "Inclusion Criteria"
    and not "Exclusion Criteria"

define "Patient Age Less Than 18":
  if (Config."Age Less than 18 Years Is Enabled") then
    AgeInYearsAt(Today()) < 18
  else
    false

define "Inclusion Criteria":
  "Patient Is Being Prescribed Opioid Analgesic with Ambulatory Misuse Potential"
    and not "Patient Age Less Than 18"
    and Routines."Is Opioid Review Useful?"
    and (not "Patient had Urine Screening in Last 12 Months"
      or "Has Positive Screening for Cocaine in Last 12 Months"
      or "Has Positive Screening for PCP in Last 12 Months")

define "Exclusion Criteria":
  Common."End of Life Assessment"

define "Patient had Urine Screening in Last 12 Months":
  exists( "Urine Screenings during the Last 12 Months" )

define "Urine Screenings during the Last 12 Months":
  "Non-opioid Screenings"
    union "Opioid Screenings"
    union "Cocaine Screenings"
    union "PCP Screenings"

/*
every pos date in last 12 months then count # neg and report that
most recent POS PCP/cocaine
 then get subsequent neg date > pos based on date of each neg and pos
*/

define "Non-opioid Screenings":
  GetRelevantScreenings(
    [Observation: "category" in Common."Observation Category Laboratory"] LabObservations
      where (LabObservations.code in Common."Non-opioid drug urine screening")
  )

define "Opioid Screenings":
  GetRelevantScreenings(
    [Observation: "category" in Common."Observation Category Laboratory"] LabObservations
      where (LabObservations.code in Common."Opioid drug urine screening")
  )

define "Cocaine Screenings":
  GetRelevantScreenings(
    [Observation: "category" in Common."Observation Category Laboratory"] LabObservations
      where (LabObservations.code in Common."Cocaine Urine Tests")
  )

define "PCP Screenings":
  GetRelevantScreenings(
    [Observation: "category" in Common."Observation Category Laboratory"] LabObservations
      where (LabObservations.code in Common."PCP Urine Tests")
  )


define function GetRelevantScreenings(obsList List<Observation>):
  obsList LabObservations
     where date from LabObservations.effective in day of "Lookback Year"
       and not (LabObservations.status.value in { 'unknown', 'entered-in-error', 'cancelled' })

// Returns a text representation of a dateTime using the CQL `ToString` function.
// @param d - a FHIR dateTime to get text for
// @returns {System.String} the text representation of the dateTime
define function DateTimeText(d FHIR.dateTime):
  ToString(d.value)

define "Positive PCP Screenings":
  "PCP Screenings" PCP where
    StartsWith(Lower(PCP.value as FHIR.string), 'pos')

define "Negative PCP Screenings":
  "PCP Screenings" PCP where
    StartsWith(Lower(PCP.value as FHIR.string), 'neg')

define "Positive Cocaine Screenings":
  "Cocaine Screenings" Cocaine where
    StartsWith(Lower(Cocaine.value as FHIR.string), 'pos')

define "Negative Cocaine Screenings":
  "Cocaine Screenings" Cocaine where
    StartsWith(Lower(Cocaine.value as FHIR.string), 'neg')

/*
  borrowed from CDS4CPM CDS_Connect_Commons_for_FHIRv400
  Shoudl this go into OpioidCDSCommon?
*/
define function MostRecent(ObsList List<Observation>):
  Last(ObsList O sort by Coalesce(
    (effective as FHIR.dateTime).value,
    (effective as FHIR.Period)."end".value,
    (effective as FHIR.Period)."start".value,
    issued.value)
  )

define "Negative PCP Screenings Count Since Last Positive Screening":
  Count(
    "Negative PCP Screenings" N where
      DateTimeText(N.effective) >  DateTimeText("MostRecent"("Positive PCP Screenings").effective)
  )

define "Negative Cocaine Screenings Count Since Last Positive Screening":
  Count(
    "Negative Cocaine Screenings" N where
      DateTimeText(N.effective) >  DateTimeText("MostRecent"("Positive Cocaine Screenings").effective)
  )

define "Positive Cocaine Dates in Lookback Period":
  "Positive Cocaine Screenings" CS
    return DateTimeText(CS.effective)

define "Positive PCP Dates in Lookback Period":
  "Positive PCP Screenings" PS
    return DateTimeText(PS.effective)

define "Has Positive Screening for Cocaine in Last 12 Months":
  exists "Cocaine Screenings" CS where
     StartsWith(Lower(CS.value as FHIR.string), 'pos')

define "Has Positive Screening for PCP in Last 12 Months":
  exists "PCP Screenings" PCP where
     StartsWith(Lower(PCP.value as FHIR.string), 'pos')

define "Applicable Because of Positive Cocaine or PCP":
  "Is Recommendation Applicable?"
    and ("Has Positive Screening for Cocaine in Last 12 Months" or "Has Positive Screening for PCP in Last 12 Months")

define "Applicable Not Because of Positive Cocaine or PCP":
  "Is Recommendation Applicable?"
    and not "Has Positive Screening for Cocaine in Last 12 Months"
    and not "Has Positive Screening for PCP in Last 12 Months"

define "Detail":
  if "Applicable Because of Positive Cocaine or PCP"
    then
      (
        if ("Has Positive Screening for Cocaine in Last 12 Months" and "Has Positive Screening for PCP in Last 12 Months")
          then "Cocaine And PCP Summary"
        else if "Has Positive Screening for Cocaine in Last 12 Months"
          then "Cocaine Summary"
        else if "Has Positive Screening for PCP in Last 12 Months"
          then "PCP Summary"
        else
          'No Details Available'
      )
  else if "Applicable Not Because of Positive Cocaine or PCP"
    then 'Patients on opioid therapy should have a urine drug test performed every 12 months.'
    else null

define "Indicator":
  'warning'

define "Summary":
  if "Applicable Because of Positive Cocaine or PCP"
    then 'Positive Cocaine or PCP in Urine Screening'
  else if "Applicable Not Because of Positive Cocaine or PCP"
    then 'Annual Urine Screening Check'
  else null

/*
PlanDefinition:
  Inclusion Criteria:
    No Screening in Last 12 months - ServiceRequest - Urine Screening
    Opioids found in urine screening
    Illicit drugs found in urine screening
*/

// Service Request - Urine Screening
// Detected Issue - Opioids found in urine screening
// Detected Issue - Illicit drugs found in urine screening

define "Urine Drug Screening Request":
    ServiceRequest {
      //identifier:,
      instantiatesCanonical: { FHIR.canonical { value: 'http://fhir.org/guides/cdc/activitydefinition/urine-screening-request' } },
      status: FHIR.ServiceRequestStatus { value: 'draft' },
      intent: FHIR.ServiceRequestIntent { value: 'proposal' },
      priority: FHIR.ServiceRequestPriority { value: 'routine' },
      code: FHIR.CodeableConcept { coding: { ToCoding("Urine Drug Screening") } },
      subject: FHIR.Reference { reference: FHIR.string { value: 'Patient/' + Patient.id } },
      occurrence: FHIR.Period { start: FHIR.dateTime { value: Today() }, end: FHIR.dateTime { value: Today() + 7 days } },
      authoredOn: FHIR.dateTime { value: Now() },
      reasonCode: { FHIR.CodeableConcept { text: FHIR.string { value: "Detail" } } }
      // doesn't really work, need a relatedArtifact here...
      //reasonCode: { FHIR.CodeableConcept { text: FHIR.string { value: 'https://www.cdc.gov/mmwr/volumes/65/rr/rr6501e1.htm#10_When_prescribing_opioids' } } }
    }

/*
define "Opioids Issue":
  DetectedIssue {
    //identifier:,
    status: FHIR.ObservationStatus { value: 'preliminary' },
    code: FHIR.CodeableConcept { text: FHIR.string { value: "Evidence of Opioids Summary" } },
    severity: FHIR.DetectedIssueSeverity { value: 'moderate' },
    patient: FHIR.Reference { reference: FHIR.string { value: 'Patient/' + Patient.id } },
    identified: FHIR.dateTime { value: Now() },
    // TODO: Device representation...
    // author:,
    // TODO: Reference the lab results that were used to infer this
    // implicated:,
    detail: FHIR.string { value: "Evidence of Opioids Detail" },
    reference: FHIR.uri { value: 'http://fhir.org/guides/cdc/opioid-cds/PlanDefinition/opioidcds-10-patient-view'}
  }

define "Illicit Drugs Issue":
  DetectedIssue {
    //identifier:,
    status: FHIR.ObservationStatus { value: 'preliminary' },
    code: FHIR.CodeableConcept { text: FHIR.string { value: "Evidence of Illicit Drugs Summary" } },
    severity: FHIR.DetectedIssueSeverity { value: 'moderate' },
    patient: FHIR.Reference { reference: FHIR.string { value: 'Patient/' + Patient.id } },
    identified: FHIR.dateTime { value: Now() },
    // TODO: Device representation...
    // author:,
    // TODO: Reference the lab results that were used to infer this
    // implicated:,
    detail: FHIR.string { value: "Evidence of Illicit Drugs Detail" },
    reference: FHIR.uri { value: 'http://fhir.org/guides/cdc/opioid-cds/PlanDefinition/opioidcds-10-patient-view'}
  }
*/

define function ToCoding(code System.Code):
  FHIR.Coding {
    code: FHIR.code { value: code.code },
    system: FHIR.uri { value: code.system },
    version: FHIR.string { value: code.version },
    display: FHIR.string { value: code.display }
  }

define "Cocaine And PCP Summary":
  'Positive for Cocaine AND PCP <br/><br/>' +
    "Cocaine Summary" + '<br/>' + "PCP Summary"

define "PCP Summary":
  'Positive for PCP: <br/>' +
  'Positive lab dates: <br/>' + Combine("Positive PCP Dates in Lookback Period", ', <br/>') + '<br/>' +
  'Negative test count since last Positive: ' + ToString("Negative PCP Screenings Count Since Last Positive Screening")

define "Cocaine Summary":
  'Positive for Cocaine: <br/>' +
  'Positive lab dates: <br/>' + Combine("Positive Cocaine Dates in Lookback Period", '<br/>') + '<br/>' +
  'Negative test count since last Positive: ' + ToString("Negative Cocaine Screenings Count Since Last Positive Screening")
